class PongGame {

    static PongGame instance; // Una instancia del Juego.
    field Bat bat;            // El bat.
    field Ball ball;   
    field Laser laser;        // La bola.
    field int wall;           // La pared actual en la que rebota la pelota.
    field boolean exit;       // true cuando el juego termina.
    field int score;          // El score actual.
    field int lastWall;       // La última pared de la que rebotó la pelota.
    field int velocidad;

    // El ancho actual del bat
    field int batWidth;

    // Construye un nuevo juego.
    constructor PongGame new() {
	    do Screen.clearScreen();
        let batWidth = 50;  // Tamaño inicial del bat
        let bat = Bat.new(230, 229, batWidth, 7);
        let ball = Ball.new(253, 100, 0, 511);
        do ball.setDestination(400);    // Destino inicial de la bola
        let laser = Laser.new(255, 210, 0);
        do Screen.setColor(true);
        do Screen.drawRectangle(0, 238, 511, 240);
	    do Output.moveCursor(22,0);
	    do Output.printString("Score: 0");
        do Output.moveCursor(22,10);
	    do Output.printString("Speed: X0");
	
	    let exit = false;
	    let score = 0;
	    let wall = 0;
	    let lastWall = 0;
        let velocidad = 0;

        return this;
    }

    // Desasigna la memoria del objeto.
    method void dispose() {
        do bat.dispose();
	    do ball.dispose();
        do Memory.deAlloc(this);
        return;
    }

    // Crea una instancia del juego y la almacena.
    function void newInstance() {
        let instance = PongGame.new();
        return;
    }
    
    // Devuelve la instancia única de este juego de Pong.
    function PongGame getInstance() {
        return instance;
    }

    // Inicia el juego y maneja las entradas del usuario que 
    // controlan la dirección del movimiento del bat.
    method void run() {
        var char key;

        while (~exit) {
            // Espera a que se presione una tecla.
            while ((key = 0) & (~exit)) {
                let key = Keyboard.keyPressed();
                do bat.move();
                do moveBall();
                do laser.move();
                do verificarColision();
                do Sys.wait(50);
            }
            if (key = 131){ do moveLaser(); do laser.setUnico(true); }
            else {
                if (key = 130) { do bat.setDirection(1); }
	            else {
	                if (key = 132) { do bat.setDirection(2); }
		            else {
                        if (key = 81) { let exit = true; }
		            }
                } 
            }

            // Espera a que se deje de presionar la tecla.
            while ((~(key = 0)) & (~exit)) {
                let key = Keyboard.keyPressed();
                do bat.move();
                do moveBall();
                do laser.move();
                do Sys.wait(50);
            }
        }

	    if (exit) {
            do Output.moveCursor(12,27);
	        do Output.printString("Game Finished!");
	    }
            
        return;
    }

    // Maneja el movimiento de la bola, incluido el rebote.
    // Maneja el movimiento del laser.
    // Si la pelota rebota en una pared, le da una nueva dirección.
    // Si el laser colisiona con la bola, aumenta el puntaje en uno,
    // coloca la bola en otra zona de la pantalla y
    // aumenta la velocidad de la misma, para que el juego sea más desafiante.

    method void moveBall() {
        var int bouncingDirection;

        let wall = ball.move();

        if ((wall > 0) & (~(wall = lastWall))) {
            let lastWall = wall;
            let bouncingDirection = 0;
            do ball.bounce(bouncingDirection);
        }
        return;
    }

    method void moveLaser() {
        var int batLeft, batWidth, posx;
        var boolean unico;

        let batLeft = bat.getLeft();
        let batWidth = bat.getWidth();
        let unico = laser.getUnico();
        let posx = batLeft + (batWidth/2);

        if (~unico) {
            do laser.setX(posx);
        }
        do laser.setDirection(1);

        return;
    }

    method void verificarColision() {
        var int ballTop, ballBottom, ballLeft, ballRight, laserTop, laserBottom, laserLeft, laserRight;
        var boolean colision;

        let colision = false;
        let ballTop = ball.getTop();
        let ballBottom = ball.getBottom();
        let ballLeft = ball.getLeft();
        let ballRight = ball.getRight();

        let laserTop = laser.getTop();
        let laserBottom = laser.getBottom();
        let laserLeft = laser.getLeft();
        let laserRight = laser.getRight();

        if ((((laserLeft > ballLeft) & (laserRight < ballRight)) | ((laserLeft > ballLeft) & (laserLeft < ballRight)) | ((laserLeft < ballLeft) & (laserRight > ballLeft))) & (((laserTop < ballBottom) & (laserBottom > ballBottom)) | ((laserTop < ballBottom) & (laserBottom > ballTop)) | ((laserTop < ballTop) & (laserBottom > ballTop)))) {
            let score = score + 1;
            let colision = true;
            do laser.setColision();
            do ball.setColision();
            let velocidad = velocidad + 1;
            do ball.setVelocidad(velocidad);
            do ballBoom();
            do Output.moveCursor(22,7);
            do Output.printInt(score);
            do Output.moveCursor(22,18);
            do Output.printInt(velocidad);
        }

        return;
    }

    method void ballBoom() {
        var int ballLeft;

        let ballLeft = ball.getLeft();

        do Output.moveCursor(12,32);
	    do Output.printString("¡¡¡¡BOOM!!!!");
        do ball.hide();
        do Ball.wait(5);

        if (ballLeft < 128) {
            do ball.setDestination(384);
            do ball.show();
        }
        if ( ((ballLeft > 128) | (ballLeft = 128)) & (ballLeft < 256) )  {
            do ball.setDestination(28);
            do ball.show();
        }
        if ( ((ballLeft > 256) | (ballLeft = 256)) & (ballLeft < 384) )  {
            do ball.setDestination(480);
            do ball.show();
        }
        if ( ((ballLeft > 384) | (ballLeft = 384)) & (ballLeft < 512) )  {
            do ball.setDestination(128);
            do ball.show();
        }

        return;
    }

}