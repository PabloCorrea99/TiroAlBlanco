/**
  * Un gráfico bat.
  * Se muestra como un rectángulo horizontal y un cuadrado en la parte superior del mismo, los cuales tienen
  * una ubicación de pantalla, un ancho y una altura.
  * Tiene métodos para dibujar, borrar, mover hacia la izquierda y hacia la derecha, establecer una direccion,
  * obtener la direccion y obtener las posiciones de cada uno de sus lados.
 */
class Bat {

    field int x, y;           // La pocision en la pantalla de el bat
    field int width, height;  // El ancho y la altura del bat
    field int direction;      // Dirección del movimiento del bat (1 = izquierda, 2 = derecha)

    // Construye un nuevo bat con la ubicación y el ancho que nos dieron.
    constructor Bat new(int Ax, int Ay, int Awidth, int Aheight) {
        let x = Ax;
        let y = Ay;
        let width = Awidth;
        let height = Aheight;
        let direction = 2;
        do show();
        return this;
    }

    // Desasigna la memoria del objeto.
    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    // Muestra el bat.
    method void show() {
        do Screen.setColor(true);
        do draw();
        return;
    }

    // Oculta el bat.
    method void hide() {
        do Screen.setColor(false);
        do draw();
        return;
    }

    // Dibuja el bat.
    method void draw() {
        do Screen.drawRectangle(x, y, x + width, y + height);
        do Screen.drawRectangle(x + (width/2) - 5, y - 10, x + (width/2) + 5, y);
        return;
    }

    // Establece la dirección del bat (0 = parar, 1 = izquierda, 2 = derecha).
    method void setDirection(int Adirection) {
        let direction = Adirection;
        return;
    }

    // Devuelve la direccion de el bat.
    method int getDirection() {
        return direction;
    }

    // Devuelve el borde izquierdo del bat.
    method int getLeft() {
        return x;
    }

    // Devuelve el borde derecho del bat.
    method int getRight() {
        return x + width;
    }

    // Devuelve el valor de el ancho del bat.
    method int getWidth() {
        return width;
    }

    // Mueve el bat un paso en la dirección a la que se dirige segun las flechas.
    method void move() {
	    if (direction = 1) {
            let x = x - 4;
            if (x < 0) { let x = 0; }
            do Screen.setColor(false);
            do Screen.drawRectangle((x + width) + 1, y, (x + width) + 4, y + height);
            do Screen.drawRectangle(x + (width/2) + 6, y - 10, x + (width/2) + 9, y - 1);
            do Screen.setColor(true);
            do Screen.drawRectangle(x, y, x + 3, y + height);
            do Screen.drawRectangle(x + (width/2) - 5, y - 10, x + (width/2) - 2, y);
        }
        else {
            let x = x + 4;
            if ((x + width) > 511) { let x = 511 - width; }
            do Screen.setColor(false);
            do Screen.drawRectangle(x - 4, y, x - 1, y + height);
            do Screen.drawRectangle(x + (width/2) - 9, y - 10, x + (width/2) - 6, y - 1);
            do Screen.setColor(true);
            do Screen.drawRectangle((x + width) - 3, y, x + width, y + height);
            do Screen.drawRectangle(x + (width/2) + 2, y - 10, x + (width/2) + 5, y);
        }
        return;
    }
}
